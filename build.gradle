plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
    id "com.modrinth.minotaur" version "2.8.10"
}

minecraft_version = project.minecraft_version
version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
}

loom {
	splitEnvironmentSourceSets()
	mods {
		"tiers" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modApi "com.terraformersmc:modmenu:${modmenu_version}"
}

processResources {
	inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    filesMatching("fabric.mod.json") {
		expand "version": project.version, minecraft_version: project.minecraft_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "bubOj4AD"
    versionNumber = "${project.version}"
    versionName = "Tiers ${project.version}"
    changelog = "[Version ${project.version}](https://github.com/Flavio6561/Tiers/wiki/Version-changelogs)"
    uploadFile = remapJar
    versionType = "beta"
    gameVersions = [project.minecraft_version]
    loaders = ["fabric", "quilt"]

    dependencies {
        required.project "P7dR8mSH"
        optional.project "mOgUt4GM"
    }
}

tasks.modrinth.dependsOn(tasks.remapJar)